apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
}

android {
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

}



//Adding action after evaluate the project
project.afterEvaluate {
    android.applicationVariants.all { variant ->
        def name = variant.name
        def testTaskName = "test${name.capitalize()}UnitTest"

        //Create task for coverage reports
        tasks.create(name: "${testTaskName}Coverage", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${name.capitalize()} build."

            //Target classes directories
            classDirectories = fileTree(
                    dir: "${project.buildDir}/intermediates/classes/${name}",
                    exclude: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewInjector*.*',
                               '**/*$ViewBinder*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*',
                               '**/DatabasePrice*.*',
                               '**/DatabaseReactiveAndroid*.*']
            )

            //Target source directories
            sourceDirectories = files(['src/main/java'].plus(android.sourceSets[name].java.srcDirs))

            //Execute jacoco task
            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")


            //Create both xml and html reports
            reports {
                xml.enabled = true
                html.enabled = true
            }
        }
    }

    coveralls {
        sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
    }

    task jacocoRootReport(type: JacocoReport) {
        dependsOn = subprojects.test
        sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
        classDirectories =  files(subprojects.sourceSets.main.output)
        executionData = files(subprojects.jacocoTestReport.executionData)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
            xml.destination = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }
    }


}